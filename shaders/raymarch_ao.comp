#version 450

#if defined(PATCH_VULKAN)
#define SET_BINDING(set_, binding_) set = set_, binding = binding_
#else
#define SET_BINDING(set_, binding_) binding = binding_
#endif

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout(push_constant) uniform Constants {
    mat4 inv_view;
    mat4 inv_projection;
    vec3 bounds_min;
    float voxel_size;
    vec3 bounds_max;
    float chunk_size;
    vec3 camera_pos;
    float pad1;
    ivec3 grid_size;
    int total_chunks;
    ivec3 chunks_dim;
    int frame_count;
    int rt_quality;
    int debug_mode;
    int is_orthographic;
    int max_steps;
    float near_plane;
    float far_plane;
    int object_count;
    int shadow_quality;
    int shadow_contact;
    int ao_quality;
    int lod_quality;
    int reserved;
} pc;

#include "include/gbuffer_sample.glsl"

ivec3 pc_grid_size;
ivec3 pc_chunks_dim;
int pc_total_chunks;

layout(std430, SET_BINDING(0, 0)) readonly buffer VoxelBuffer {
    uint voxel_data[];
};

layout(std430, SET_BINDING(0, 1)) readonly buffer ChunkHeaders {
    uvec4 chunk_headers[];
};

layout(SET_BINDING(0, 2)) uniform usampler3D shadow_volume_tex;

#include "include/data_terrain.glsl"

layout(SET_BINDING(1, 0)) uniform sampler2D gbuffer_depth;
layout(SET_BINDING(1, 1)) uniform sampler2D gbuffer_normal;
layout(SET_BINDING(1, 2)) uniform sampler2D blue_noise_tex;

layout(r8, SET_BINDING(2, 0)) writeonly uniform image2D out_ao;

const int MAX_AO_STEPS = 64;
const float AO_MAX_DIST = 4.0;

vec3 world_to_grid(vec3 world_pos) {
    return (world_pos - pc.bounds_min) / pc.voxel_size;
}

bool sample_shadow_volume(ivec3 voxel_pos) {
    ivec3 shadow_pos = voxel_pos >> 1;
    ivec3 shadow_size = textureSize(shadow_volume_tex, 0);

    if (shadow_pos.x < 0 || shadow_pos.x >= shadow_size.x ||
        shadow_pos.y < 0 || shadow_pos.y >= shadow_size.y ||
        shadow_pos.z < 0 || shadow_pos.z >= shadow_size.z) {
        return false;
    }

    uint packed = texelFetch(shadow_volume_tex, shadow_pos, 0).r;
    int bit_idx = (voxel_pos.x & 1) + ((voxel_pos.y & 1) << 1) + ((voxel_pos.z & 1) << 2);
    return (packed & (1u << bit_idx)) != 0u;
}

bool sample_shadow_mip1(ivec3 voxel_pos) {
    ivec3 mip1_pos = voxel_pos >> 2;
    ivec3 mip1_size = textureSize(shadow_volume_tex, 1);

    if (mip1_pos.x < 0 || mip1_pos.x >= mip1_size.x ||
        mip1_pos.y < 0 || mip1_pos.y >= mip1_size.y ||
        mip1_pos.z < 0 || mip1_pos.z >= mip1_size.z) {
        return false;
    }

    return texelFetch(shadow_volume_tex, mip1_pos, 1).r != 0u;
}

bool sample_shadow_mip2(ivec3 voxel_pos) {
    ivec3 mip2_pos = voxel_pos >> 3;
    ivec3 mip2_size = textureSize(shadow_volume_tex, 2);

    if (mip2_pos.x < 0 || mip2_pos.x >= mip2_size.x ||
        mip2_pos.y < 0 || mip2_pos.y >= mip2_size.y ||
        mip2_pos.z < 0 || mip2_pos.z >= mip2_size.z) {
        return false;
    }

    return texelFetch(shadow_volume_tex, mip2_pos, 2).r != 0u;
}

float trace_ao_ray(vec3 origin, vec3 rd) {
    vec2 box_hit = hdda_intersect_aabb(origin, rd, pc.bounds_min, pc.bounds_max);
    if (box_hit.x > box_hit.y || box_hit.y < 0.0) {
        return 1.0;
    }

    float t_start = max(box_hit.x, 0.001);
    float t_end = min(box_hit.y, AO_MAX_DIST);

    if (t_start >= t_end) {
        return 1.0;
    }

    vec3 start_pos = origin + rd * t_start;
    vec3 grid_pos = world_to_grid(start_pos);
    grid_pos = clamp(grid_pos, vec3(0.0), vec3(pc.grid_size) - 0.001);

    ivec3 map_pos = ivec3(floor(grid_pos));
    vec3 delta_dist = abs(1.0 / rd);
    ivec3 step_dir = ivec3(sign(rd));
    vec3 side_dist = (sign(rd) * (vec3(map_pos) - grid_pos) + sign(rd) * 0.5 + 0.5) * delta_dist;

    ivec3 last_mip2_pos = ivec3(-1000);
    ivec3 last_mip1_pos = ivec3(-1000);
    bool mip2_empty = false;
    bool mip1_empty = false;

    for (int i = 0; i < MAX_AO_STEPS; i++) {
        if (map_pos.x < 0 || map_pos.x >= pc.grid_size.x ||
            map_pos.y < 0 || map_pos.y >= pc.grid_size.y ||
            map_pos.z < 0 || map_pos.z >= pc.grid_size.z) {
            return 1.0;
        }

        vec3 hit_grid = vec3(map_pos) + 0.5;
        vec3 hit_world = pc.bounds_min + hit_grid * pc.voxel_size;
        float dist = length(hit_world - origin);
        if (dist > AO_MAX_DIST) {
            return 1.0;
        }

        ivec3 current_mip2 = map_pos >> 3;
        if (current_mip2 != last_mip2_pos) {
            last_mip2_pos = current_mip2;
            mip2_empty = !sample_shadow_mip2(map_pos);
            last_mip1_pos = ivec3(-1000);
        }

        if (mip2_empty) {
            bvec3 mask = lessThanEqual(side_dist.xyz, min(side_dist.yzx, side_dist.zxy));
            side_dist += vec3(mask) * delta_dist;
            map_pos += ivec3(mask) * step_dir;
            continue;
        }

        ivec3 current_mip1 = map_pos >> 2;
        if (current_mip1 != last_mip1_pos) {
            last_mip1_pos = current_mip1;
            mip1_empty = !sample_shadow_mip1(map_pos);
        }

        if (mip1_empty) {
            bvec3 mask = lessThanEqual(side_dist.xyz, min(side_dist.yzx, side_dist.zxy));
            side_dist += vec3(mask) * delta_dist;
            map_pos += ivec3(mask) * step_dir;
            continue;
        }

        if (sample_shadow_volume(map_pos)) {
            float ao = dist / AO_MAX_DIST;
            return ao;
        }

        bvec3 mask = lessThanEqual(side_dist.xyz, min(side_dist.yzx, side_dist.zxy));
        side_dist += vec3(mask) * delta_dist;
        map_pos += ivec3(mask) * step_dir;
    }

    return 1.0;
}

vec3 cosine_weighted_hemisphere(vec2 xi, vec3 N) {
    float phi = 2.0 * 3.14159265 * xi.y;
    float cos_theta = sqrt(1.0 - xi.x);
    float sin_theta = sqrt(xi.x);

    vec3 local_dir = vec3(
        cos(phi) * sin_theta,
        sin(phi) * sin_theta,
        cos_theta
    );

    vec3 up = abs(N.y) < 0.999 ? vec3(0.0, 1.0, 0.0) : vec3(1.0, 0.0, 0.0);
    vec3 tangent = normalize(cross(up, N));
    vec3 bitangent = cross(N, tangent);

    return tangent * local_dir.x + bitangent * local_dir.y + N * local_dir.z;
}

void main() {
    pc_grid_size = pc.grid_size;
    pc_chunks_dim = pc.chunks_dim;
    pc_total_chunks = pc.total_chunks;

    ivec2 pixel = ivec2(gl_GlobalInvocationID.xy);
    ivec2 screen_size = imageSize(out_ao);

    if (pixel.x >= screen_size.x || pixel.y >= screen_size.y) {
        return;
    }

    GBufferSample g = sample_gbuffer_shadow(
        pixel, screen_size,
        gbuffer_depth, gbuffer_normal,
        pc.inv_view, pc.inv_projection, pc.camera_pos,
        pc.is_orthographic != 0
    );

    if (g.is_sky) {
        imageStore(out_ao, pixel, vec4(1.0));
        return;
    }

    vec3 normal = normalize(g.normal);

    vec2 noise_uv = vec2(pixel) / 128.0;
    float spatial_noise = texture(blue_noise_tex, noise_uv).r;
    float temporal_offset = float(pc.frame_count) * 0.6180339887;

    float bias = pc.voxel_size * 0.6;
    vec3 ao_origin = g.world_pos + normal * bias;

    int num_samples = (pc.ao_quality >= 2) ? 2 : 1;
    float ao_sum = 0.0;

    for (int s = 0; s < num_samples; s++) {
        float noise = fract(spatial_noise + temporal_offset + float(s) * 0.5);
        float noise2 = fract(spatial_noise * 1.618 + temporal_offset + float(s) * 0.309);

        vec2 xi = vec2(noise, noise2);

        vec3 ao_dir = cosine_weighted_hemisphere(xi, normal);

        ao_sum += trace_ao_ray(ao_origin, ao_dir);
    }

    float ao = ao_sum / float(num_samples);

    imageStore(out_ao, pixel, vec4(ao, 0.0, 0.0, 0.0));
}
